apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.didi.dokit'

android {
    signingConfigs {
        release {
            storeFile file('C:\\Users\\ASUS\\Desktop\\mvvmcomponents.jks')
            storePassword '123456'
            keyAlias 'key0'
            keyPassword '123456'
        }
    }
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //阿里路由框架配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
                includeCompileClasspath = true
            }
        }
//        ndk {
//            abiFilters 'armeabi-v7a'
//        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
//            applicationIdSuffix '.debug' //增加包名后缀 （debug包与release包共存）
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isBuildModule.toBoolean()) {
                //独立运行
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
            } else {
                //合并到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时，排除debug文件夹下所有调试文件
                    exclude 'src/debug/*'
                }
            }
        }
    }
    //统一资源前缀，规范资源引用
    resourcePrefix "app_"
    dataBinding {
        enabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    dokitExt {
        //通用设置
        comm {
            //地图经纬度开关
            gpsSwitch true
            //网络开关
            networkSwitch true
            //大图开关
            bigImgSwitch true
            //webView js 抓包
            webViewSwitch true
        }
    }
}

dependencies {
    implementation project(path: ':lib_base')
    if (!isBuildModule.toBoolean()) {
        implementation project(path: ':module_login')
        implementation project(path: ':module_main')
        implementation project(path: ':module_user')
        implementation project(path: ':module_search')
        implementation project(path: ':module_square')
        implementation project(path: ':module_project')
        implementation project(path: ':module_web')
    }
}